
% Rules for handling compounds
:- consult('../formula/compounder').

% The grammar for symbolic equations
:- consult('equation.pl').

% The grammar for word equations
:- consult('word_equation.pl').

% Rebase procedure (creates lookup tables, merges re-occurences of elements in a formula, flattens polyatomic groups)
:- consult('rebase.pl').

% The solver (converts the coefficient set to a system of linear equations and solves it).
:- consult('solver.pl').

balance_equation(Equation,Solution) :- balance_equation(symbolic,Equation,symbolic,Solution).

balance_equation_real(InGrammar,Equation,OutGrammar,Solution) :-
	call(InGrammar,user,_,[],Elems,[],MoleculeSet,[],SideSet,Equation,[]),

	rebase(SideSet),
	sort(Elems,ElementSet),

	coefficient_set(MoleculeSet,ElementSet,CoeffSet),
	!,

	equation_evaluate(CoeffSet,Values),
	!,

	call(OutGrammar,output,Values,[],Elems,[],MoleculeSet,[],SideSet,Solution,[]).

/* This function just performs a cleanup */
balance_equation(InGrammar,Equation,OutGrammar,Solution) :-
	balance_equation_real(InGrammar,Equation,OutGrammar,Solution) -> retractall(balance(_,_,_));
	(retractall(balance(_,_,_)), fail).



coefficient_set(_,[],[]) :- !.

coefficient_set(MolSet,[Elem|ElemS],[CoeffSet|CoeffSetS]) :-
	element_coefficients(MolSet,Elem,CoeffSet),
	coefficient_set(MolSet,ElemS,CoeffSetS).


element_coefficients([],_,[]).

element_coefficients([Mol|MolS],Elem,[Coeff|CoeffS]) :-
      (balance(Mol,Elem,Coeff) ; Coeff = 0),
        element_coefficients(MolS,Elem,CoeffS),
        !.


% vi: syntax=prolog
